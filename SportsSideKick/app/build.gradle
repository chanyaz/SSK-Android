apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 27
        multiDexEnabled true
    }
    flavorDimensions "main"
    productFlavors {
        sportsidekick {
            applicationId "tv.sportssidekick.sportssidekick"
            versionCode 6
            versionName "6.0.0.0"
        }
        brugge {
            applicationId "tv.sportssidekick.brugge"
            versionCode 6
            versionName "5.0.0.6"
        }
    }
    lintOptions {
        abortOnError false
        checkReleaseBuilds false
        ignoreWarnings true
        quiet true
        lintOptions {
            disable 'ContentDescription', 'KeyboardInaccessibleWidget', 'MissingPrefix'
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile(
                    'proguard-android.txt'),
                    'proguard-rules.pro'
        }
    }
}

kotlin {
    experimental {
        coroutines "enable"
    }
    kapt {
        useBuildCache = true
    }
}

dependencies {
    // Android support libraries
    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support:design:27.0.2'
    implementation 'com.android.support:recyclerview-v7:27.0.2'
    implementation 'com.android.support:cardview-v7:27.0.2'
    implementation 'com.android.support:percent:27.0.2'
    implementation 'com.android.support:customtabs:27.0.2'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    // Push notifications
    implementation 'com.google.firebase:firebase-messaging:11.8.0'
    implementation project(':e-goi-push-notifications')
    implementation 'com.google.android.gms:play-services-base:11.8.0'
    implementation 'com.google.android.gms:play-services-gcm:11.8.0'

    // Youtube
    implementation fileTree(include: ['YouTubeAndroidPlayerApi.jar'], dir: 'libs')
    implementation 'com.google.http-client:google-http-client-android:1.20.0'
    implementation 'com.google.api-client:google-api-client-android:1.20.0'
    implementation 'com.google.api-client:google-api-client-gson:1.20.0'
    implementation 'com.google.apis:google-api-services-youtube:v3-rev181-1.22.0'
    implementation files('libs/YoutubeAndroidPlayerApi.jar')

    // Gamesparks backend
    implementation 'com.gamesparks.sdk:gamesparks-android-client-sdk:0.4.5'

    // Json parsing
    implementation 'com.fasterxml.jackson.core:jackson-core:2.9.0'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.9.0'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.9.0'

    // Facebook
    implementation 'com.facebook.android:facebook-android-sdk:4.24.0'
    implementation('com.facebook.android:audience-network-sdk:4.24.0') {
        exclude group: 'com.google.android.gms'
    }

    // Image uploading
    implementation 'com.amazonaws:aws-android-sdk-core:2.4.2'
    implementation 'com.amazonaws:aws-android-sdk-s3:2.4.2'

    // Circle Progress UI element used for Profile icon
    implementation 'com.github.lzyzsd:circleprogress:1.2.1'

    // Custom fonts
    implementation 'uk.co.chrisjenx:calligraphy:2.3.0'

    // Shared preferences wrapper
    implementation 'com.pixplicity.easyprefs:library:1.8.1@aar'

    // Parses HTML store items or stats for sharing
    implementation 'org.jsoup:jsoup:1.11.2'

    // Reactive extensions
    implementation 'io.reactivex.rxjava2:rxjava:2.1.8'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'com.github.miguelbcr:RxPaparazzo:0.6.0-2.x'

    // VideoView with controls
    implementation 'com.linsea:universalvideoview:1.1.0@aar'

    // Image loader
    implementation 'com.github.bumptech.glide:glide:4.6.1'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.6.1'

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'org.jetbrains.anko:anko:0.10.4'

    // Architecture components
    implementation 'android.arch.lifecycle:extensions:1.1.0'

    // Transparent status bar
    implementation 'com.jaeger.statusbarutil:library:1.4.0'

    // Video chat
    implementation 'com.twilio:video-android:1.3.0'

    // Checkout (Android In-App Billing Library) used for billing
    implementation 'org.solovyev.android:checkout:1.0.0'

    // List animations
    implementation 'jp.wasabeef:recyclerview-animators:2.3.0'


    // *** Dev tools *** \\

    // Crash reporting
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.0@aar') {
        transitive = true
    }

    // Detecting memory leaks
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'


    // *** Replaceable with Kotlin or RxJava, deprecated libraries *** \\

    // Binds Android views and callbacks to fields and methods
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    // Permission handling library used around the project for easier handling of permissions
    implementation 'com.github.hotchemi:permissionsdispatcher:3.0.1'
    annotationProcessor 'com.github.hotchemi:permissionsdispatcher-processor:3.0.1'

    // Event bus
    implementation 'org.greenrobot:eventbus:3.0.0'

    // Picker used for translation popup to select language
    implementation 'com.shawnlin:number-picker:2.4.4'
}

apply plugin: 'com.google.gms.google-services'